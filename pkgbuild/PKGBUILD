# Maintainer: Julian Pollinger <julian@pollinger.dev>

pkgname=('backup-checker')
pkgver=0.3.2
pkgrel=1
pkgdesc='Check if all files where backed up by comparing their checksums.'
arch=('x86_64')
url='https://github.com/neferin12/backup-checker/'
license=('MIT')
depends=()
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('35b45a67d160dc211aa5aa8fcd7f1a69c185d5de6b17f73d3f9e0798a108b8cb')

prepare() {
    cd "$pkgname-$pkgver" || exit 1
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$pkgname-$pkgver" || exit 1
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

package() {
    cd "$pkgname-$pkgver" || exit 1
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}
